=== List of procedures


==== Miscelaneous

<procedure>(++ string1 string2 ... stringn)</procedure>

A shortcut to {{string-append}}.


<procedure>(concat args #!optional (sep ""))</procedure>

Convert {{args}} to string and intersperse the resulting strings with {{sep}}.


<procedure>(awful-version)</procedure>

Return the awful version (a string).


==== Javascript

<procedure>(include-javascript . files)</procedure>

A shortcut to {{(<script> type: "text/javascript" src: file)}}.


<procedure>(add-javascript . code)</procedure>

Add arbitrary javascript code to the pages defined by {{define-page}} and {{define-session-page}}.


==== Debugging

<procedure>(debug . args)</procedure>

Print {{args}}, concatenated, to the file {{debug-file}}.


<procedure>(debug-pp arg)</procedure>

Pretty-print {{arg}} to the file {{debug-file}}.



==== Sessions and authentication

<procedure>($session var #!optional default)</procedure>

Return the value of {{var}} in the session (or {{default}} if {{var}} does not exist or is {{#f}}).


<procedure>($session-set! var #!optional val)</procedure>

If {{var}} is a quoted symbol, set the value of {{var}} to {{val}}.  If {{val}} is not provided, {{var}} will have its value set to {{#f}}.

{{var}} can be an alist mapping session variable names to their corresponding values.

Examples:

<enscript highlight=scheme>
($session-set! 'foo "foo value")

($session-set! '((foo . "foo value")
                 (bar . "bar value")
                 (baz . "baz value")))
</enscript>


<procedure>(link url text . rest)</procedure>

Return a session-aware HTML code for a link, using the {{<a>}} procedure from [[/egg/html-tags|html-tags]].

The {{rest}} arguments are the same as the ones for the {{<a>}} procedure from [[/egg/html-tags|html-tags]], plus the following:

; {{no-session}} : a boolean.  If {{#t}}, forces {{link}} to ignore the session even when {{enable-session}} is {{#t}}.
; {{arguments}} : an alist mapping variable names to their corresponding values, to be passed to uri-common's {{form-urlencode}} procedure.
; {{separator}} : the value to the {{separator}} keyword argument to be passed to to uri-common's {{form-urlencode}} procedure.

When {{enable-session}} is {{#t}}, {{link}} automatically encodes the session identifier in the URI (unless {{no-session}} is {{#t}}).


<procedure>(form contents . rest)</procedure>

Return a session-aware HTML code for a form, using the {{<form>}} procedure from [[/egg/html-tags|html-tags]].

The {{rest}} arguments are the same as the ones for the {{<form>}} procedure from [[/egg/html-tags|html-tags]], plus {{no-session}}, a boolean.  If {{no-session}} is {{#t}}, it forces {{form}} to ignore the session even when {{enable-session}} is {{#t}}.

When {{enable-session}} is {{#t}}, {{form}} automatically generates a hidden input field to pass the session identifier (unless {{no-session}} is {{#t}}).


<procedure>(define-login-trampoline path #!key vhost-root-path hook)</procedure>

Define a trampoline -- an intermediate page accessed when redirecting from the login page to the main page.


<procedure>(login-form #!key (user-label "User: ") (password-label "Password: ") (submit-label "Submit") (refill-user #t))</procedure>

Return a user/password login form (e.g., for using in authentication pages).

When the {{refill-user}} is {{#t}}, the User field is reffiled with the value from the {{user}} query string value when either the session or the password is invalid.

The {{user-label}}, {{password-label}} and {{submit-label}} keyword parameters are labels to be used for the user, password and submit form widgets, respectively.



==== Request variables and values

<procedure>($ var #!optional default/converter)</procedure>

Return the HTTP request value for the given variable {{var}}.  The variable is looked for in both the query string (GET method) and request body (e.g., POST method).  See the documentation for the procedure returned by [[/eggref/4/spiffy-request-vars|spiffy-request-vars]]'s {{request-vars}} for further details.


==== Database access

<procedure>($db q #!key default values)</procedure>

Execute the given query ({{q}}) on the database and return the result as a list of lists or {{default}} if the result set is empty.

The {{values}} keyword parameter (a list) is a list of values to replace the placehoders in the query.

Example:

<enscript highlight=scheme>
($db "insert into foo (bar, baz) values (?, ?)" values: '("bar-val" "baz-val"))
</enscript>

<procedure>($db-row-obj q)</procedure>

Execute the given query {{q}} on the database and return an one-argument procedure which takes as argument the name of the database field to get the value.

Example:

<enscript highlight=scheme>
(let ((& ($db-row-obj "select full_name, phone from users where user_id=1")))
  (<p> "Full name: " (& 'full_name))
  (<p> "Phone: " (& 'phone)))
</enscript>

''Warning'': currently {{$db-row-obj}} is only implemented for Postgresql databases.


<procedure>(sql-quote . data)</procedure>

Escape and quote the concatenation of {{data}} to be used in SQL queries.

''Warning'': for Sqlite databases, {{sql-quote}} just replaces {{'}} by {{''}} and quotes the {{data}}.  For Postgresql, {{sql-quote}} quotes the result of {{escape-string}}.



==== Pages

<procedure>(define-page path contents #!key css title doctype headers charset no-ajax use-ajax no-template no-session no-db no-javascript-compression)</procedure>

Define an awful page.

{{path}} is the path to the page.  It can be represented by two types: a string and a regular expression object.  If it is a string, the path used in the URI will be bound to the given no-argument procedure {{contents}}.  If it is a regular expression object, any request whose URL path matches the regular expression will br handled by the one-argument procedure {{contents}}.  This procedure will be given the requested path.

The {{css}}, {{title}}, {{doctype}}, {{headers}} and {{charset}} keyword parameters have the same meaning as {{html-page}} (from the [[/egg/html-utils|html-utils]] egg).

If {{no-ajax}} is {{#t}}, it means that the page won't use ajax, even if the {{enable-ajax}} parameter is {{#t}}.

If {{use-ajax}} is {{#t}}, it means that the page will be linked to the ajax library, even if the {{enable-ajax}} parameter is {{#f}}.

If {{no-template}} is {{#t}}, it means that no page template (see the {{page-template}} parameter) should be used.

If {{no-session}} is {{#t}}, it means that the page should not use session.

If {{no-db}} is {{#t}}, it means that the page should not use the database, even when database usage is activated by {{enable-db}} and {{db-credentials}} is not {{#f}}.

If {{no-javascript-compression}} is {{#t}} the javascript code for the page is not compressed, even when {{enable-javascript-compression}} is not {{#f}}.

Examples:

<enscript highlight=scheme>

(use srfi-1 ;; for filter-map
     regex) ;; for regexp

;; http://host:port/foo => "bar"
(define-page "/foo"
  (lambda ()
    "bar"))

;; http://host:port/add/1/2/3 => 6
(define-page (regexp "/add/.*")
  (lambda (path)
    (let ((numbers (filter-map string->number (string-split path "/"))))
      (number->string (apply + numbers)))))

</enscript>


<procedure>(define-session-page path contents . rest)</procedure>

Define a session-aware page.  When the page is accessed and a correponding session does not exist, it is created.  If the session already exists and is not valid, it is recreated.  If the session already exists and is valid, then it is refreshed.

The {{rest}} parameters are the same as for {{define-page}}.

Here's an example (the [[http://www.paulgraham.com/arcchallenge.html|arc challenge]]):

<enscript highlight=scheme>
(use awful html-utils spiffy-request-vars)

(define-session-page "said"
  (lambda ()
    (with-request-vars* $ (said)
      (cond (said
             ($session-set! 'said said)
             (link "said" "click here"))
            (($session 'said)
             => (lambda (said)
                  (++ "You said: " said)))
            (else (form (++ (text-input 'said)
                            (submit-input))
                        action: "said"
                        method: 'post))))))
</enscript>


<procedure>(undefine-page path #!optional vhost-root-path)</procedure>

Undefine a page whose path is {{path}} (a string or a regular expression object).

The optional parameter {{vhost-root-path}} is the path of virtual host where the page is to be undefined.  If omited, {{(root-path)}} is used.


==== Ajax

<procedure>(ajax path selector event proc #!key target (action 'html) (method 'POST) (arguments '()) success no-session no-db vhost-root-path live prelude update-targets cache)</procedure>

Generate javascript code to be added to the page defined by {{define-page}}.  Return the generated javascript code (which usually is not useful, so should be discarded).

{{path}} is the URL path (a string) of the server side handler. This path is placed under the {{(app-root-path)/(ajax-namespace)}} path.  So, if your {{app-root-path}} is {{"my-app"}}, your {{ajax-namespace}} is {{"ajax"}} and you use {{"do-something"}} as the first argument to {{ajax}}, the URL for the server side handler would be {{"/my-app/ajax/do-something"}}.

{{selector}} is the selector for the DOM element to be observed.  If it is a quoted symbol, awful generates a JQuery selector by DOM id (e.g., {{'my-selector}} generates {{"#my-selector"}}).  If it is a string, awful uses it as-is to generate the JQuery selector (e.g., {{"input[name^=omg]"}} generates {{"input[name^=omg]"}}).

{{event}} (a quoted symbol or a list) is the event(s) to be observed. If it is a quoted symbol (e.g., {{'click}}), only this event will be bound.  If {{event}} is a list of events, all the events from the list will be bound.

{{proc}} is a no-argument procedure to be executed on the server side.

The {{target}} keyword parameter is the id of the DOM element to be affected by the result of {{proc}}.

The {{method}} (a quoted symbol, usually {{'GET}} or {{'POST}}) keyword parameter is the HTTP method to be used by the ajax request.

The {{arguments}} keyword parameter is an alist mapping request variables (symbols) to their values (strings).  {{ajax}} uses these arguments to assembly the query string or the request body to send to the server when performing the ajax request.

Example:

<enscript highlight=scheme>
arguments: '((var1 . "$('#var1').val()")
             (var2 . "$('#var2').val()"))
</enscript>

If the {{no-session}} keyword parameter is {{#t}}, it means that no session should be considered ({{ajax}} implicit sends the session identifier when {{no-session}} is {{#f}}).

If the {{no-db}} keyword parameter is {{#t}}, it means that the should be no attempt to connect the database, even when database usage is activated by {{enable-db}} and {{db-credentials}} is not {{#f}}.

The {{vhost-root-path}} keyword parameter (a string) is the vhost root path.  It is useful for explicitly separate pages defined using the same path (see {{define-page}}) but for different vhosts.

The {{live}} keyword parameter (boolean) indicates wheter ajax should use JQuery's live method (see [[http://api.jquery.com/live/]]).

The {{prelude}} keyword parameter (string) is an arbitrary piece of javascript code to be placed right before the ajax request.

The {{update-targets}} keyword parameter a boolean indicating whether multiple targets should be updated upon ajax response.  When {{update-targets}} is used, the procedure {{proc}} used as argument to {{ajax}} should yield an alist as result.  The alist maps DOM elements identifiers to their corresponding values.

Here's an example:

<enscript highlight=scheme>
#!/usr/bin/awful

(use awful html-tags)

(enable-ajax #t)

(define-page (main-page-path)
  (lambda ()

    (ajax "foo" 'foo 'click
          (lambda ()
            '((a . 1) (b . 2) (c . 3)))
          update-targets: #t)

    (<div>
     (link "#" "foo" id: "foo")
     (<div> id: "a")
     (<div> id: "b")
     (<div> id: "c"))))
</enscript>

The {{success}} keyword parameter (string) can be any arbitrary javascript code to be executed on the successful ajax request.  The javascript code can assume that a variable {{response}} is bound and contains the request resulting data.  Here's an example:

<enscript highlight=scheme>
#!/usr/bin/awful

(use awful html-tags)

(enable-ajax #t)

(define-page (main-page-path)
  (lambda ()

    (ajax "foo" 'foo "click"
          (lambda ()
            "hey")
          success: "$('#bar').html(response + ' you!')")

    (++ (link "#" "foo" id: "foo")
        (<div> id: "bar"))))
</enscript>

The {{cache}} keyword parameter (boolean), if set to {{#f}}, it will force requested pages not to be cached by the browser.  The default value is not set, leaving it to be set by JQuery. See [[http://api.jquery.com/jQuery.ajax/|JQuery's documentation]] for further details.

The {{ajax}} procedure is session, HTTP request and database -aware.


<procedure>(periodical-ajax path interval proc #!key target (action 'html) (method 'POST) (arguments '()) success no-session no-db vhost-root-path live prelude update-targets)</procedure>

Periodically execute {{proc}} on the server side, using {{(app-root-path)/(ajax-namespace)/path}} as the URL path for the server side handler.

{{interval}} (a number) is the interval between consecutive executions of {{proc}}, in milliseconds.

The meaning of the keyword parameters is the same as for {{ajax}}'s.


<procedure>(ajax-link path id text proc #!key target (action 'html) (method 'POST) (arguments '()) success no-session no-db (event 'click) vhost-root-path live class hreflang type rel rev charset coords shape accesskey tabindex a-target prelude update-targets)</procedure>

A shortcut to

<enscript highlight=scheme>
(begin
  (ajax path id 'click proc ...)
  (<a> href: "#" [...other <a> keyword parameters...] id: id text))
</enscript>

The meaning of the {{target}}, {{action}}, {{method}}, {{arguments}}, {{success}}, {{no-session}}, {{no-db}}, {{event}}, {{vhost-root-path}}, {{update-targets}} and {{live}} keyword parameters is the same as for {{ajax}}'s.

The meaning of the {{class}}, {{hreflang}}, {{type}}, {{rel}}, {{rev}}, {{charset}}, {{coords}}, {{shape}}, {{accesskey}}, {{tabindex}} and {{a-target}} are the same as for [[/egg/html-tags|html-tags]]' {{<a>}} procedure (except that {{a-target}} is {{<a>}}'s {{target}}, since {{ajax}} uses the {{target}} keyword parameter).

The {{event}} keyword parameter syntax is the same for {{ajax}}'s {{event}} mandatory parameter.


==== Redirection

<procedure>(redirect-to uri)</procedure>

Perform an HTTP redirection (code 302) to the given {{uri}} (either a string or a [[/eggref/4/uri-common|uri-common]] uri object).  To be used from {{define-page}} contents.  Example:

<enscript highlight=scheme>
(use awful)

;; / -> /foo
(define-page "/"
  (lambda ()
    (redirect-to "/foo")))

(define-page "/foo"
  (lambda ()
    "foo"))
</enscript>

The example above shows a redirection from {{/}} to {{/foo}}.  Redirections can also be performed when the origin path is a regular expression:

<enscript highlight=scheme>
(use awful)

;; /bar.* -> /foo
(define-page (regexp "/bar.*")
  (lambda (_)
    (redirect-to "/foo")))

(define-page "/foo"
  (lambda ()
    "foo"))
</enscript>


==== Web REPL

<procedure>(enable-web-repl path #!key css title)</procedure>

Enable the web REPL.  {{path}} is the URL path to the web REPL.

The keyword parameter {{css}} is the CSS to be used the the web REPL page (see the documentation for {{html-page}}, from the [[/egg/html-utils|html-utils]] egg, for the {{css}} keyword parameter.)

The keyword parameter {{title}} (a string) is the title for the web REPL page (see the documentation for {{html-page}}, from the [[/egg/html-utils|html-utils]] egg, for the {{title}} keyword parameter.)

The web REPL is automatically enabled by the awful application server when the {{--development-mode}} is provided (available from {{/web-repl}}).  By default, the fancy editor is used, but can be disabled with the {{--disable-web-repl-fancy-editor}} command line option for the awful application server.


==== Session inspector

<procedure>(enable-session-inspector path #!key css title)</procedure>

Enable the session inspector.  {{path}} is the URL path to the session inspector.

The keyword parameter {{css}} is the CSS to be used the the session inspector page (see the documentation for {{html-page}}, from the [[/egg/html-utils|html-utils]] egg, for the {{css}} keyword parameter.)

The keyword parameter {{title}} (a string) is the title for the session inspector page (see the documentation for {{html-page}}, from the [[/egg/html-utils|html-utils]] egg, for the {{title}} keyword parameter.)

The session inspector is automatically enabled by the awful application server when the {{--development-mode}} is provided (available from {{/session-inspector}}).

